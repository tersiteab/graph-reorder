element Vertex end
element Edge end

const edges : edgeset{Edge}(Vertex,Vertex) = load ("../test/graphs/4.el");
const vertices : vertexset{Vertex} = edges.getVertices();

const num_paths : vector{Vertex}(double) = 0;
const dependences : vector{Vertex}(double) = 0;
const visited : vector{Vertex}(bool) = false;

func forward_update(src : Vertex, dst : Vertex)
    num_paths[dst] +=  num_paths[src];
end

func visited_vertex_filter(v : Vertex) -> output : bool
    output = (visited[v] == false);
end

func mark_visited(v : Vertex)
    visited[v] = true;
end


func mark_unvisited(v : Vertex)
    visited[v] = false;
end

func backward_vertex_f(v : Vertex)
    % print "backward_vertex v: ";
    % print v;
    visited[v] = true;
    dependences[v] += 1 / num_paths[v];
end

func backward_update(src : Vertex, dst : Vertex)
    %print "backward_vertex_update src: ";
    %print src;

    %print "backward_vertex_update dst: ";
    %print dst;
    dependences[dst] += dependences[src];
end

func final_vertex_f(v : Vertex)
    if num_paths[v] != 0
        dependences[v] = (dependences[v] - 1 / num_paths[v]) * num_paths[v];
    else
        dependences[v] = -1;
    end
end

func print_num_paths(v : Vertex)
    print num_paths[v];
end

func print_dependences(v : Vertex)
    print dependences[v];
end

func main()

    var frontier : vertexset{Vertex} = new vertexset{Vertex}(0);
    frontier.addVertex(2);
    num_paths[2] = 1;
    visited[2] = true;
    var round : int = 0;
    var frontier_list : list{vertexset{Vertex}} = new list{vertexset{Vertex}}();
    frontier_list.append(frontier);

    % foward pass to propagate num_paths

    while (frontier.getVertexSetSize() != 0)
        round = round + 1;
        print "round";
        print round;

        print "size ";
        print frontier.getVertexSetSize();
        var output : vertexset{Vertex} = edges.from(frontier).to(visited_vertex_filter).applyModified(forward_update, num_paths);
        output.apply(mark_visited);
        frontier_list.append(output);
        frontier = output;
    end

    % print "-------num_paths-------";
    % vertices.apply(print_num_paths);

    % transposing the edges
    var transposed_edges : edgeset{Edge}(Vertex, Vertex) = edges.transpose();

    % resetting the visited information for the backward pass
    vertices.apply(mark_unvisited);

    % pop off the empty frontier
    frontier = frontier_list.pop();
    delete frontier;

    % start from the last frontier
    frontier = frontier_list.pop();
    frontier.apply(backward_vertex_f);
    round = round - 1;

    % backward pass to accumulate the dependencies

    while (round > 0)
        print "size ";
        print frontier.getVertexSetSize();
        transposed_edges.from(frontier).to(visited_vertex_filter).apply(backward_update);
        delete frontier;
        frontier = frontier_list.pop();
        frontier.apply(backward_vertex_f);
        round = round - 1;

    end
    delete frontier;
    vertices.apply(final_vertex_f);

    % print "-------dependences-------";
    % vertices.apply(print_dependences);

    var sum : double = 0;
    for i in 0: edges.getVertices()
        if dependences[i] > 0
            sum += dependences[i];
        end
    end

    print "sum: ";
    print sum;
end
